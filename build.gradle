// Based on:
// https://github.com/beryx-gist/badass-jlink-example-log4j2-javafx
//
// The jlink task will produce build/image/bin/hwX-tester (a shell script)
// The jlinkZip task will produce build/hwX-tester.zip
//
// More info: https://badass-jlink-plugin.beryx.org/releases/latest/

plugins {
    id 'java-library'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.17.0'
}

repositories {
    mavenCentral()

    flatDir {
        dirs '.'
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation ':CircuitSim1.8.2-austin:'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    implementation 'org.junit.jupiter:junit-jupiter-params:5.5.2'
    implementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    implementation 'org.junit.platform:junit-platform-launcher:1.5.2'
}

javafx {
    version = '13'
    modules = ['javafx.controls']
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

mainClassName = 'tester/edu.gatech.cs2110.circuitsim.launcher.TesterLauncher'

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    imageZip = file("$buildDir/hwX-tester.zip")
    launcher {
        name = 'hwX-tester'
    }
}

javadoc {
    title 'CircuitSim Autograder API'
    include 'edu/gatech/cs2110/circuitsim/api/*', 'edu/gatech/cs2110/circuitsim/extension/*'
    // For github pages
    destinationDir = file('docs')
    options.addBooleanOption 'html5', true
    options.addStringOption  'link', 'https://docs.oracle.com/javase/10/docs/api/'
}
